
plugins {
    id 'java' // or 'groovy' Must be explicitly applied
    id "com.github.johnrengelman.shadow" version "2.0.2"
}


def localBuildFile = "./build-local.gradle"
def useLocalBuild = new File(localBuildFile).exists()
if (useLocalBuild) {
    apply from: localBuildFile
}

defaultTasks 'shadowJar'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''Suggestions-Tracker: This needs Java 8. You are using something else, try again.'''.stripMargin())
}

// Define a version registry so that we can avoid hard coding of the version numbers
// Note that this applies only to dependencies and not to the plugins and build
ext {
    vertxVersion = '3.5.0'
    slf4jVersion = '1.7.12'
    junitVersion = '4.12'
    logbackVersion = '1.0.13'
    hikariVersion = '2.7.6'
    postgresVersion = '42.2.6'
    jdbiVersion = '2.78'
    kafkaVersion = '0.10.1.1'
}

version = "0.1-snapshot"

tasks.withType(JavaCompile) {
    //enable compilation in a separate daemon process
    options.fork = true
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    //enable incremental compilation
    options.incremental = true
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    // Vertx specific dependencies
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-hazelcast:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"
    compile "io.vertx:vertx-dropwizard-metrics:${vertxVersion}"

    // Other dependencies
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "com.zaxxer:HikariCP:${hikariVersion}"
    compile "org.postgresql:postgresql:${postgresVersion}"
    compile "io.vertx:vertx-redis-client:${vertxVersion}"
    compile "org.jdbi:jdbi:${jdbiVersion}"
    compile "org.apache.kafka:kafka-clients:${kafkaVersion}"
    // Test dependencies
    testCompile "junit:junit:${junitVersion}"
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'org.gooru.suggestions.bootstrap.SuggestionsRunner'
        attributes 'Created-By': 'Gooru.org'
    }
}

defaultTasks 'shadowJar'

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
